import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/buildingSchedule/actions.pkl" as Actions
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/buildingSchedule/context.pkl" as Context
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/buildingSchedule/guards.pkl" as Guards
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/events.pkl" as Events
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/serviceTypes.pkl" as ServiceTypes
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/variables.pkl" as Vars
import "https://raw.githubusercontent.com/CollaborativeStateMachines/Cirrina/refs/heads/develop/src/main/resources/pkl/csm/Csml.pkl"

/// This module defines the roomScheduleSM.
/// It listens to the global schedule and manages local user overrides.
/// One instance of this SM will run for each room or zone.
class roomScheduleSM extends Csml.StateMachineDescription {
  name = "roomSchedule"
  localContext = Context.roomContext
  states {
    init
    active
    inactive
    warning
    authorizedStay
    emergency
  }
}

local const init: Csml.StateDescription = new {
  name = "init"
  initial = true
  entry {
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stInitializeZone
      output { new { reference = Vars.vZoneId } }
      done { new { name = "zoneInitialized" channel = "internal" } }
    }
  }

  on {
    new {
      event = "zoneInitialized"
      target = inactive.name
    }
  }
}

/// The room is active
local const active: Csml.StateDescription = new {
  name = "active"
  entry {
    Actions.aRaiseZoneActive
  }
  on {
    // The buildingScheduleSM signifies the building is entering afterHours or weekend
    // Start the graceful shutdown warning.
    new {
      event = Events.eEnterAfterHoursMode
      target = warning.name
    }
    new {
      event = Events.eEnterWeekend
      target = warning.name
    }
    new {
      event = Events.eFireAlarm
      target = emergency.name
    }
  }
}

/// The room is closed - waits for building to open or user to request stay
local const inactive: Csml.StateDescription = new {
  name = "inactive"
  entry {
    Actions.aRaiseZoneInactive
  }
  on {
    new {
      event = Events.eEnterBusinessHours
      target = active.name
    }
    new {
      event = Events.eFireAlarm
      target = emergency.name
    }
    new {
      event = Events.eRequestStay
      guards { Guards.requestedStay }
      target = authorizedStay.name
    }
    new {
      event = Events.eGasLeakDetected
      target = emergency.name
    }
  }
}

/// Grace period state for building closure
local const warning: Csml.StateDescription = new {
  name = "warning"
  entry {
    Actions.aRaiseZoneWarning
  }
  after {
    new Csml.TimeoutActionDescription {
      name = "gracePeriodTimer"
      delay = "300000" // 5 minutes
      action = Actions.aRaiseShutdownRoom
    }
  }
  on {
    // The occupant responded to the warning.
    new {
      event = Events.eRequestStay
      guards { Guards.requestedStay }
      target = authorizedStay.name
    }
    // The timer expired without response
    new {
      event = Events.eShutdownRoom
      target = inactive.name
    }
    // The schedule changed back to open before the timer finished.
    new {
      event = Events.eEnterBusinessHours
      target = active.name
    }
    new {
      event = Events.eFireAlarm
      target = emergency.name
    }
  }
}

/// A person has temporarly requested an extended stay
local const authorizedStay: Csml.StateDescription = new {
  name = "authorizedStay"
  entry {
    Actions.aRaiseZoneActive
  }
  // Person gets a 2 hour lease, before re-evaluation
  after {
    new Csml.TimeoutActionDescription {
      name = "overrideLeaseTimer"
      delay = "7200000"
      action = Actions.aRaiseRecheckOverride
    }
  }
  on {
    new {
      event = Events.eRecheckOverride
      target = warning.name
    }
    // The person has left
    new {
      event = Events.eUserLeftZone
      guards { Guards.userLeftZone }
      target = inactive.name
    }
    // The building is open now, lease no longer required
    new {
      event = Events.eEnterBusinessHours
      target = active.name
    }
    new {
      event = Events.eFireAlarm
      target = emergency.name
    }
  }
}

/// Safety protocols override all schedules.
local const emergency: Csml.StateDescription = new {
  name = "emergency"
  on {
    // When the alarm is disarmed, go to 'Inactive' and wait for the re-broadcast of current schedule
    new {
      event = Events.eDisarmFireAlarm
      target = inactive.name
    }
  }
}
