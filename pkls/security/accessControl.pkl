import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/events.pkl" as Events
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/security/actions.pkl" as Actions
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/security/context.pkl" as Context
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/security/guards.pkl" as Guards
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/serviceTypes.pkl" as ServiceTypes
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/variables.pkl" as Vars
import "https://raw.githubusercontent.com/CollaborativeStateMachines/Cirrina/refs/heads/develop/src/main/resources/pkl/csm/Csml.pkl"

/// This module defines the accessControlSM for a single access-controlled door.
class accessControlSM extends Csml.StateMachineDescription {
  name = "accessControl"
  localContext = Context.accessControlContext
  states {
    init
    locked
    authenticating
    checkingRules
    unlocked
    accessDenied
    emergencyUnlock
    forcedOpenAlarm
  }
}

local const init: Csml.StateDescription = new {
  name = "init"
  initial = true
  entry {
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stGetDoorRouteType
      input {
        new {
          name = "doorId"
          value = Vars.vDoorId } }
      output {
        new {
          reference = Vars.vIsEvacuationRoute } }
      done {
        new {
          name = "doorTypeDetermined"
          channel = "internal" }
      }
    }
  }
  on { new { event = "doorTypeDetermined" target = locked.name } }
}

/// Default locked state, listens for access attempts and overrides.
local const locked: Csml.StateDescription = new {
  name = "locked"
  on {
    new {
      event = Events.eAuthenticationRequest
      guards { Guards.isCurrentDoor }
      target = authenticating.name
    }
    new { event = Events.eFireAlarm target = emergencyUnlock.name }
    new { event = Events.eGasLeakDetected guards { Guards.checkIsEvacuationRoute } target = emergencyUnlock.name }
    new { event = Events.eUnlockAllEvacuationRoutes guards { Guards.checkIsEvacuationRoute } target = emergencyUnlock.name }
    new {
      event = Events.eLockdownZone
      guards { Guards.isCurrentZone }
    }
    new { event = Events.eDoorForcedOpen target = forcedOpenAlarm.name }
    new { event = Events.ePhysicalTamper target = forcedOpenAlarm.name }
    new { event = Events.eForceUnlockRequest target = unlocked.name }
  }
}

/// Calls the authentication service.
local const authenticating: Csml.StateDescription = new {
  name = "authenticating"
  entry {
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stAuthenticateUser
      input { new { name = "cardId" value = "$cardId" } }
      output {
        new { reference = Vars.vUserId }
        new { reference = Vars.vUserRole }
        new { reference = Vars.vAuthenticationStatus }
      }
      done { new { name = "authComplete" channel = "internal" } }
    }
  }
  on {
    new {
      event = "authComplete"
      guards { Guards.authSuccessful }
      target = checkingRules.name
    }
    new {
      event = "authComplete"
      guards { Guards.authFailed }
      actions {
        Actions.aRaiseAccessDeniedAuth
      }
      target = accessDenied.name
    }
    new { event = Events.eFireAlarm target = emergencyUnlock.name }
  }
}

/// Calls the rule checking service based on authenticated role and current schedule.
local const checkingRules: Csml.StateDescription = new {
  name = "checkingRules"
  entry {
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stCheckAccessRule
      input {
        new { name = "userRole" value = Vars.vUserRole }
        new { name = "zoneId" value = Vars.vZoneId }
        new { name = "currentScheduleMode" value = Vars.vCurrentScheduleMode }
      }
      output { new { reference = Vars.vAccessDecision } }
      done { new { name = "rulesChecked" channel = "internal" } }
      // Add onError handling
    }
  }
  on {
    new {
      event = "rulesChecked"
      guards { Guards.allowAccess }
      actions {
        Actions.aRaiseAccessGranted
      }
      target = unlocked.name
    }
    new {
      event = "rulesChecked"
      guards { Guards.denyAccess }
      actions {
        Actions.aRaiseAccessDeniedRule
      }
      target = accessDenied.name
    }
    new { event = Events.eFireAlarm target = emergencyUnlock.name }
  }
}

/// Door is temporarily unlocked.
local const unlocked: Csml.StateDescription = new {
  name = "unlocked"
  entry {
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stControlDoorLock
      input {
        new { name = "doorId" value = Vars.vDoorId }
        new { name = "command" value = "'unlock'" }
      }
    }
  }
  after {
    new Csml.TimeoutActionDescription {
      name = "autoRelockTimer"
      delay = "10000" // 10 seconds
      action = Actions.aRaiseAutoRelock
    }
  }
  on {
    new { event = "autoRelock" target = locked.name }
    new { event = Events.eFireAlarm target = emergencyUnlock.name }
    new {
      event = Events.eLockdownZone
      guards { Guards.isCurrentZone }
      target = locked.name
    }
    new { event = Events.eDoorForcedOpen target = forcedOpenAlarm.name }
    new { event = Events.ePhysicalTamper target = forcedOpenAlarm.name }
  }
}

/// Access was denied, briefly show status, then re-lock.
local const accessDenied: Csml.StateDescription = new {
  name = "accessDenied"
  after {
    new Csml.TimeoutActionDescription {
      name = "deniedDelay"
      delay = "3000" // 3 seconds
      action = Actions.aRaiseReturnToLock
    }
  }
  on {
    new { event = "returnToLock" target = locked.name }
    new { event = Events.eFireAlarm target = emergencyUnlock.name }
  }
}

/// Highest priority state, forces door unlocked.
local const emergencyUnlock: Csml.StateDescription = new {
  name = "emergencyUnlock"
  entry {
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stControlDoorLock
      input {
        new { name = "doorId" value = Vars.vDoorId }
        new { name = "command" value = "'unlock'" }
      }
    }
  }
  on {
    new { event = Events.eDisarmFireAlarm target = locked.name }
    new { event = Events.eGasPurged target = locked.name }
  }
}

/// Alarm state if physical tampering or forced entry is detected.
local const forcedOpenAlarm: Csml.StateDescription = new {
  name = "forcedOpenAlarm"
  entry {
    Actions.aRaiseForcedEntry
    new Csml.InvokeActionDescription {
      serviceType = ServiceTypes.stControlDoorLock
      input {
        new { name = "doorId" value = Vars.vDoorId }
        new { name = "command" value = "'lock'" }
      }
    }
  }
  on {
    new { event = Events.eClearSecurityAlert target = locked.name }
    new { event = Events.eFireAlarm target = emergencyUnlock.name }
  }
}
