import "https://raw.githubusercontent.com/CollaborativeStateMachines/Cirrina/refs/heads/develop/src/main/resources/pkl/csm/Csml.pkl"
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/events.pkl" as Events
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/serviceTypes.pkl" as ServiceTypes
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/variables.pkl" as Vars
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/shading/guards.pkl?v=1" as Guards
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/shading/actions.pkl" as Actions
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/shading/context.pkl" as Context

// This module contains the definition for the shading SM

// The shading SM contains six states:
// half, open, close, userLevel, emergency + an initialization state
// The state transitions are controlled by outside temperature detection, except for userLevel, which is determined by the user
class shadingSM extends Csml.StateMachineDescription {
    name = "shading"
    localContext = Context.blindLockContext
    states {
        sInit
        sHalf
        sOpen
        sClose
        sUserLevel
        sEmergency
    }
}

local const sInit: Csml.StateDescription = new {
    name = "init"
    initial = true
    always {
        new {
            target = sHalf.name
        }
    }
}
// Initially the SM always transitions to half
local const sHalf: Csml.StateDescription = new {
    name = "half"
    entry {
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stBlindsHalf
        }
    }
    on {
        // Depending on temperature readings transitions to open or clsoe take place
        // Given that the blinds are not locked
        new {
            event = Events.eTempLow
            guards {
                Guards.isLockedGuard
            }
            target = sOpen.name
            actions{
                new Csml.AssignActionDescription {
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        new {
            event = Events.eTempHigh
            guards {
                Guards.isLockedGuard
            }
            target = sClose.name
            actions{
                new Csml.AssignActionDescription {
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        // Transition to userLevel when an event with a manual level is received
        new {
            event = Events.eActivateBlindsUserLevel
            target = sUserLevel.name
        }
        new {
            event = Events.eLockBlinds
            actions {
                Actions.aBlindLock
            }
        }
        new {
            event = Events.eUnlockBlinds
            actions {
                Actions.aBlindUnlock
            }
        }
        // On Fire/Smoke events, transition to emergency state
        new {
            event = Events.eFireAlarm
            target = sEmergency.name
        }
        new {
            event = Events.eSmokeAlert
            target = sEmergency.name
        }
    }
}

// Blinds open when temperature is low
local const sOpen: Csml.StateDescription = new {
    name = "open"
    entry {
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stBlindsOpen
        }
    }
    on {
        // Transitions to half/close depending on temperature
        // Given blinds are not locked
        new {
            event = Events.eTempMedium
            guards {
                Guards.isLockedGuard
            }
            target = sHalf.name
            actions{
                new Csml.AssignActionDescription {
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    } 
                }
            }
        }
        new {
            event = Events.eTempHigh
            guards {
                Guards.isLockedGuard
            }
            target = sClose.name
            actions{
                new Csml.AssignActionDescription {
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        new {
            event = Events.eActivateBlindsUserLevel
            target = sUserLevel.name
        }
        new {
            event = Events.eLockBlinds
            actions {
                Actions.aBlindLock
            }
        }
        new {
            event = Events.eUnlockBlinds
            actions {
                Actions.aBlindUnlock
            }
        }
        new {
            event = Events.eFireAlarm
            target = sEmergency.name
        }
        new {
            event = Events.eSmokeAlert
            target = sEmergency.name
        }
    }
}

// Blinds close when temperature is high
local const sClose: Csml.StateDescription = new {
    name = "close"
    entry {
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stBlindsClose
        }
    }
    on {
        // Transition to half, open depending on temperature
        // Given blinds are not locked
        new {
            event = Events.eTempMedium
            guards {
                Guards.isLockedGuard
            }
            target = sHalf.name
            actions{
                new Csml.AssignActionDescription {
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        new {
            event = Events.eTempLow
            guards {
                Guards.isLockedGuard
            }
            target = sOpen.name
            actions{
                new Csml.AssignActionDescription {
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                    
                }
            }
        }
        new {
            event = Events.eActivateBlindsUserLevel
            target = sUserLevel.name
        }
        new {
            event = Events.eLockBlinds
            actions {
                Actions.aBlindLock
            }
        }
        new {
            event = Events.eUnlockBlinds
            actions {
                Actions.aBlindUnlock
            }
        }
        new {
            event = Events.eFireAlarm
            target = sEmergency.name
        }
        new {
            event = Events.eSmokeAlert
            target = sEmergency.name
        }
    }
    
}

// Blinds open to manual user level
// Manual level can be saved between outwards transitions and eventual re-entry.
// If user level event with level -1 is received, previous manual level will be used. 
local const sUserLevel: Csml.StateDescription = new {
    name = "userLevel"
    staticContext {
        variables{
            Vars.vUserBlindLevel
        }
    }
    entry {
        new Csml.AssignActionDescription {
        variable {
            name = Vars.vUserBlindLevel.name
            value = "if ($blindLevel != -1) $blindLevel else blindLevel"
        }
    }
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stBlindsUserLevel
            input {
                new {
                    name = "blindLevel"
                    value = "blindLevel"
                }
            }
        }
    }
    on {
        // Assign temperature readings in the background, for accurate exit on deactivation of manual level
        new {
            event = Events.eTempLow
            actions{
                new Csml.AssignActionDescription{
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        new {
            event = Events.eTempMedium
            actions {
                new Csml.AssignActionDescription{
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        new {
            event = Events.eTempHigh
            actions{
                new Csml.AssignActionDescription{
                    variable {
                        name = Vars.vTemperature
                        value = "$temp"
                    }
                }
            }
        }
        new {
            event = Events.eLockBlinds
            actions {
                Actions.aBlindLock
            }
        }
        new {
            event = Events.eUnlockBlinds
            actions {
                Actions.aBlindUnlock
            }
        }
        // Once manual level is deactivated, transition to correct target depending on current temperature
        new {
            event = Events.eDeactivateBlindsUserLevel
            guards {
                Guards.isColdGuard
            }
            target = sOpen.name
        }
        new {
            event = Events.eDeactivateBlindsUserLevel
            guards {
                Guards.isMediumGuard
            }
            target = sHalf.name
        }
        new{
            event = Events.eDeactivateBlindsUserLevel
            guards {
                Guards.isHotGuard
            }
            target = sClose.name
        }
        new {
            event = Events.eFireAlarm
            target = sEmergency.name
        }
        new {
            event = Events.eSmokeAlert
            target = sEmergency.name
        }
    }
    
}

// Emergency state is entered once smoke or fire alarm is triggered
// Blinds completely open until manual disarming of the alarm
local const sEmergency: Csml.StateDescription = new {
    name = "emergency"
    entry {
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stBlindsUserLevel
            input {
                new {
                    name = "blindLevel"
                    value = "100"
                }
            }
        }
    }
    on {
        new {
            event = Events.eTempLow
            guards {
                Guards.noFireAlarm
                Guards.noSmokeAlert
            }
            target = sOpen.name
        }
        new {
            event = Events.eTempMedium
            guards {
                Guards.noFireAlarm
                Guards.noSmokeAlert
            }
            target = sHalf.name
        }
        new {
            event = Events.eTempHigh
            guards {
                Guards.noFireAlarm
                Guards.noSmokeAlert
            }
            target = sClose.name
        }
        new {
            event = Events.eActivateBlindsUserLevel
            guards {
                Guards.noFireAlarm
                Guards.noSmokeAlert
            }
            target = sUserLevel.name
        }
    }
}