import "https://raw.githubusercontent.com/CollaborativeStateMachines/Cirrina/refs/heads/develop/src/main/resources/pkl/csm/Csml.pkl"
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/events.pkl" as Events
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/serviceTypes.pkl" as ServiceTypes
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/variables.pkl" as Vars
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/gasSafety/guards.pkl" as Guards
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/gasSafety/context.pkl" as Context
import "https://raw.githubusercontent.com/AlexZangerle/csmba/main/pkls/gasSafety/actions.pkl" as Actions

// This module contains the definition for the gasSafety SM

//gasSafety is the third part of the fire prevention system
//gasSafety detects gas leak across multiple rooms
//If a gas leak is detected, the location of that gas leak and a warning is published
class gasSafetySM extends Csml.StateMachineDescription {
    name = "gasSafety"
    localContext = Context.gasSafetyContext
    states {
        init
        monitoring
        gasLeak
        acknowledged
    }
}

local const init: Csml.StateDescription = new {
    name = "init"
    initial = true
    always {
        new {
            target = monitoring.name
        }
    }
}

// Always starts in monitoring state
// Invokes gas detection service every 30 seconds
local const monitoring: Csml.StateDescription = new {
    name = "monitoring"
    after {
        new Csml.TimeoutActionDescription {
            name = "checkGasLeak"
            delay = "30000"
            action = Actions.aRaiseStartGasLeakDetection
        }
    }
    on {
        new {
            event = Events.eStartGasLeakDetection
            actions {
                new Csml.InvokeActionDescription {
                serviceType = ServiceTypes.stCheckGasLeak
                output {
                    new {
                        reference = Vars.vGasLeakLocation
                    }
                }
                done {
                    new {
                        name = Events.eGasLeakDetectionComplete
                        channel = "internal"
                    }
                }
            }   
            }
        }
        // Once gas leak has been detected, transition to gasLeak state
        new {
            event = Events.eGasLeakDetectionComplete
            guards {
                Guards.foundGasLeak
            }
            target = gasLeak.name
        }
    }
}

// Invokes close valve service and raises global gas leak event
local const gasLeak: Csml.StateDescription = new {
    name = "gasLeak"
    entry {
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stCloseGasValve
            input {
                new {
                    name = "gasLeakLocation"
                    value = Vars.vGasLeakLocation
                }
            }
        }
        Actions.aRaiseGasLeakDetected
    }
    on {
        new {
            event = Events.eGasPurged
            target = acknowledged.name
        }
    }
}

local const acknowledged: Csml.StateDescription = new {
    name = "acknowledged"
    entry {
        new Csml.InvokeActionDescription {
            serviceType = ServiceTypes.stGasLeakPurged
        }
    }
    always {
        new {
            target = monitoring.name
        }
    }
}
